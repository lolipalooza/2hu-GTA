{======================================================================
======================================================================}
const
    CC_POSX = 0
    CC_POSY = 4
    CC_CLICKX = 8
    CC_CLICKY = 12
    CC_CFLAG = 16
    
    CC_SIZE_X = 12.0
    CC_SIZE_Y = 12.0
end

:InitCursorControl
0390: load_txd_dictionary 'ftend'
038F: load_texture "smouse" as 1
03E0: draw_text_behind_textures 1
0AB1: @StoreData 3 @CursorControlVars 320.0 CC_POSX
0AB1: @StoreData 3 @CursorControlVars 224.0 CC_POSY
0AB2: ret 0

:CursorControl
0AB1: @GetData 2 @CursorControlVars CC_POSX 12@
0AB1: @GetData 2 @CursorControlVars CC_POSY 13@
0A4A: store_joystick_X_offset_to 10@ Y_offset_to 11@
if 12@ < 0.0
then 12@=0.0
else
    if 12@ > 640.0
    then 12@=640.0
    else 0059: 12@ += 10@ // (float)
    end
end
if 13@ < 0.0
then 13@=0.0
else
    if 13@ > 448.0
    then 13@=448.0
    else 0063: 13@ -= 11@  // (float)
    end
end
03E3: set_texture_to_be_drawn_antialiased 1
10@=CC_SIZE_X
11@=CC_SIZE_Y
10@/=2.0
11@/=2.0
005B: 10@ += 12@  // (float)
005B: 11@ += 13@  // (float)
038D: draw_texture 1 position 10@ 11@ size 12.0 12.0 RGBA 255 255 255 255
0AB1: @StoreData 3 @CursorControlVars 12@ CC_POSX
0AB1: @StoreData 3 @CursorControlVars 13@ CC_POSY
if 00E1:   player 0 pressed_key PAD_FIREWEAPON // CLICK
then
    0AB1: @StoreData 3 @CursorControlVars 12@ CC_CLICKX
    0AB1: @StoreData 3 @CursorControlVars 13@ CC_CLICKY
else
    0AB1: @GetData 2 @CursorControlVars CC_CFLAG 14@
    if 14@ == 1
    then 0AB1: @StoreData 3 @CursorControlVars 0 CC_CFLAG
    end
end
0AB2: ret 0

:CursorControlVars
hex
    00 00 00 00 // X position
    00 00 00 00 // Y position
    00 00 00 00 // X clicked
    00 00 00 00 // Y clicked
    00 00 00 00 // click flag
end

{======================================================================
======================================================================}
:CursorClickControl
0AB2: ret 0

{======================================================================
======================================================================}
:CursorGetCorners
// Inputs: 0@ 1@: center of the box, 2@ 3@: size of the box
2@/=2.0
3@/=2.0
0087: 4@ = 0@ // (float)
0063: 4@ -= 2@  // (float)
0087: 5@ = 1@ // (float)
0063: 5@ -= 3@  // (float)
0087: 6@ = 0@ // (float)
005B: 6@ += 2@  // (float)
0087: 7@ = 1@ // (float)
005B: 7@ += 3@  // (float)
0AB2: ret 4 superior_left_corner 4@ 5@ inferior_right_corner 6@ 7@

{======================================================================
======================================================================}
:CursorClickCheck
// 0@: corner 1 x-point
// 1@: corner 1 y-point
// 2@: corner 2 x-point
// 3@: corner 2 y-point
0AB1: @CursorPositionCheck 4 rectangle_center 0@ 1@ rectangle_size 2@ 3@
0AB1: @GetData 2 @CursorControlVars CC_CLICKX 12@
0AB1: @GetData 2 @CursorControlVars CC_CLICKY 13@
0AB1: @CursorGetCorners 4 rectangle_center 0@ 1@ rectangle_size 2@ 3@ return_values superior_left_corner 0@ 1@ inferior_right_corner 2@ 3@

0AB1: @GetData 2 @CursorControlVars CC_CFLAG 14@
if or
14@ == 1
80E1:   not player 0 pressed_key PAD_FIREWEAPON // CLICK
then
    059A:  return_false
    0AB2: ret 0
end

if and
0035:   12@ >= 0@  // (float)
0035:   2@ >= 12@  // (float)
0035:   13@ >= 1@  // (float)
0035:   3@ >= 13@  // (float)
then
    0485:  return_true
    0AB1: @StoreData 3 @CursorControlVars 1 CC_CFLAG
else 059A:  return_false
end
0AB2: ret 0

{======================================================================
======================================================================}
:CursorPositionCheck
// Inputs: 0@ 1@: center of the box, 2@ 3@: size of the box
0AB1: @GetData 2 @CursorControlVars CC_POSX 12@
0AB1: @GetData 2 @CursorControlVars CC_POSY 13@
0AB1: @CursorGetCorners 4 rectangle_center 0@ 1@ rectangle_size 2@ 3@ return_values superior_left_corner 4@ 5@ inferior_right_corner 6@ 7@
if and
0035:   12@ >= 4@  // (float)
0035:   6@ >= 12@  // (float)
0035:   13@ >= 5@  // (float)
0035:   7@ >= 13@  // (float)
then 038E: draw_box_position 0@ 1@ size 2@ 3@ RGBA 0 0 0 100
end
0AB2: ret 0
