{======================================================================
======================================================================}
:SexMenuOptions_SVLAMD
hex
// Bed
"S_BJ_1" "S_HJ_1" "S_FK_1" "S_AN_1" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"

// Sofa
"S_CN_1" "S_BJ_1" "S_69_1" "______" "______"
"S_FK_1" "S_AN_1" "______" "______" "______"
"S_HJ_1" "S_TJ_1" "S_FJ_1" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"

// Table
"S_FK_1" "S_AN_1" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"

// Chair
"S_BJ_1" "S_TJ_1" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"

// Floor
"S_CN_1" "S_BJ_1" "______" "______" "______"
"S_FK_1" "S_AN_1" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"

// Bath
"S_AN_1" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"

// Kitchen
"S_BJ_1" "S_FK_1" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"

// Wall
"S_FK_1" "S_AN_1" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"

// On Foot
"S_HJ_1" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
"______" "______" "______" "______" "______"
end

{======================================================================
======================================================================}
:ShowSexualMenu
// Pink Box Title
038E: draw_box_position 199.0 106.0 size 347.0 43.0 RGBA 255 147 251 80
038E: draw_box_position 206.0 111.0 size 346.0 43.0 RGBA 255 147 251 40
038E: draw_box_position 201.0 105.0 size 323.0 52.0 RGBA 255 147 251 70
038E: draw_box_position 202.0 115.0 size 355.0 11.0 RGBA 255 147 251 80
DummyString = 'CFG_T7' // Change position
0AB1: @SetDrawText 8 font 0 linewidth 600.0 size 0.6 2.5 position 66.0 92.0 GXT Dummy1 Dummy2

// Black boxes options
038E: draw_box_position 318.0 250.0 size 592.0 262.0 RGBA 0 0 0 80
038E: draw_box_position 318.0 250.0 size 607.0 275.0 RGBA 0 0 0 50
038E: draw_box_position 326.0 250.0 size 593.0 288.0 RGBA 0 0 0 80
038E: draw_box_position 596.0 250.0 size 51.0 276.0 RGBA 0 0 0 100
038E: draw_box_position 300.0 369.0 size 541.0 23.0 RGBA 0 0 0 100
038E: draw_box_position 318.0 350.0 size 607.0 16.0 RGBA 255 255 255 10
038E: draw_box_position 149.0 223.0 size 222.0 179.0 RGBA 0 0 0 100
038E: draw_box_position 438.0 236.0 size 333.0 204.0 RGBA 0 0 0 100

// The "choose" pose button box
038E: draw_box_position 545.0 420.0 size 154.0 30.0 RGBA 255 147 251 60
038E: draw_box_position 538.0 414.0 size 154.0 30.0 RGBA 255 147 251 50
038E: draw_box_position 541.0 416.0 size 139.0 48.0 RGBA 255 147 251 60
038E: draw_box_position 542.0 422.0 size 161.0 6.0 RGBA 255 147 251 100
DummyString = 'S_OPT4' // Change!
0AB1: @SetDrawText 8 font 2 linewidth 640.0 size 0.3 1.5 position 524.0 409.0 GXT Dummy1 Dummy2
DummyString = 'S_OPT5' // Cancel
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 430.0 GXT Dummy1 Dummy2

0AB1: @SexualMenu_GetContent2 0
0AB1: @SexualMenu_CheckBoxes 2 SexPlace SexPoseInt

// Change Button Click
if 0AB1: @CursorClickCheck 4 box_position 542.0 416.0 box_size 162.0 48.0
then
    0AB1: @GetInt 1 @SxMn_Pose Dummy1
    0AB1: @GetInt 1 @SxMn_Place Dummy2
    0AB1: @SxMn_GetGXTPose 2 place Dummy2 offset Dummy1 ret Dummy3 Dummy4
    0AB1: @GetPoseFromString 2 Dummy3 Dummy4 ret Dummy1
    if or
    803B:   not Dummy1 == SexPoseInt  // (int)
    803B:   not Dummy2 == SexPlace  // (int)
    then
        0085: SexPoseInt = Dummy1 // (int)
        0085: SexPlace = Dummy2 // (int)
        fade 0 time 800
        wait 800 ms
        015F: set_camera_position 0.0 0.0 0.0 rotation 0.0 0.0 0.0
        0160: set_camera_point_at 0.0 0.0 0.0 switchstyle 2
        009B: destroy_actor PornActress
        009B: destroy_actor PornActor
        0AB1: call_scm_func @GenerateActors 2 SexPoseInt SexPlace ret PornActress PornActor
        0AB1: call_scm_func @GenerateSexFiles 2 SexPoseInt SexPlace
        0AB1: call_scm_func @LoadSexualPose 5 PornActress PornActor SexPoseInt SexPlace SPD_SLOW
        0AB1: call_scm_func @ToggleSexualCamera 4 SexPoseInt SexPlace PornActress PornActor
        wait 500
        fade 1 time 800
        SexMenuState = 0 // Close Sexual Menu
    end
end
return

{======================================================================
======================================================================}
:SexualMenu_GetContent2
0AB1: @GetInt 1 @SxMn_Place 0@
// Places (Left Menu)
for 11@ = 1 to 3
    for 10@ = 1 to 3
        0AB1: @SxMn_GetIntegerFromArray 3 x_val 10@ y_val 11@ width 3 ret 20@ // i = x + (y-1)*3 - 1
        20@+=1
        0AD3: 12@v = format "S_PL_%d" 20@
        0AB1: @SxMn_GetCoord 3 index 10@ start_point 56.0 offset 64.0 ret 20@ // xpos = 56 + (x-1)*64
        0AB1: @SxMn_GetCoord 3 index 11@ start_point 152.0 offset 38.0 ret 21@ // ypos = 152 + (y-1)*38
        if 10@ == 1
        then 14@=120.0
        end
        if 10@ == 2
        then 14@=180.0
        end
        if 10@ == 3
        then 14@=260.0
        end
        0AB1: @SetDrawText 8 font 2 linewidth 14@ size 0.2 1.2 position 20@ 21@ GXT 12@ 13@
    end
end
for 11@ = 1 to 5
    for 10@ = 1 to 5
        0AB1: @SxMn_GetIntegerFromArray 3 x_val 10@ y_val 11@ width 5 ret 20@ // i = x + (y-1)*5 - 1
        0AB1: @SxMn_GetGXTPose 2 place 0@ offset 20@ ret 12@ 13@
        if 85AE:   not 12@s == "______" // @s == 'short'
        then
            0AB1: @SxMn_GetCoord 3 index 10@ start_point 276.0 offset 66.0 ret 20@ // xpos = 276 + (x-1)*66
            0AB1: @SxMn_GetCoord 3 index 11@ start_point 152.0 offset 38.0 ret 21@ // ypos = 152 + (y-1)*38
            if 10@ == 1
            then 14@=340.0
            end
            if 10@ == 2
            then 14@=420.0
            end
            if 10@ == 3
            then 14@=480.0
            end
            if 10@ == 4
            then 14@=540.0
            end
            if 10@ == 5
            then 14@=600.0
            end
            0AB1: @SetDrawText 8 font 2 linewidth 14@ size 0.2 1.2 position 20@ 21@ GXT 12@ 13@
        end
    end
end
0AB2: ret 0

{======================================================================
======================================================================}
:SxMn_GetGXTPose
0AC6: 10@ = label @SexMenuOptions_SVLAMD offset
0085: 11@ = 0@ // (int)
11@*=25
11@*=6

0085: 12@ = 1@ // (int)
12@*=6

005A: 10@ += 11@
005A: 10@ += 12@ // total_offset = label_offset + 25*6*pose + offset*6

0A8D: 20@ = read_memory 10@ size 4 virtual_protect 0
10@+=4
0A8D: 21@ = read_memory 10@ size 2 virtual_protect 0
0AB2: ret 2 gxt_entry 20@ 21@

:SxMn_GetIntegerFromArray
// x: 0@,   y: 1@,  2@: width
// i = x + (y-1)*width - 1
0085: 10@ = 1@ // (int)
000E: 10@ -= 1
006A: 10@ *= 2@  // (int)
005A: 10@ += 0@  // (int)
000E: 10@ -= 1 // i = x + (y-1)*5 - 1
0AB2: ret 1 10@

:SxMn_GetCoord
// 0@: index,   1@: start point,    2@: offset
// xpos = start + (x-1)*offset
0093: 10@ = integer 0@ to_float
000F: 10@ -= 1.0
006B: 10@ *= 2@  // (float)
005B: 10@ += 1@  // (float)
0AB2: ret 1 10@

{======================================================================
======================================================================}
:SexualMenu_CheckBoxes
// 0@ place, 1@: pose
0AB1: @GetInt 1 @SxMn_Place 30@
0AB1: @GetInt 1 @SxMn_Pose 31@
for 11@ = 1 to 3
    for 10@ = 1 to 3
        0AB1: @SxMn_GetIntegerFromArray 3 x_val 10@ y_val 11@ width 3 ret 20@ // i = x + (y-1)*3 - 1
        0AB1: @SxMn_GetCoord 3 index 10@ start_point 85.0 offset 65.0 ret 21@ // xpos = 85 + (x-1)*65
        0AB1: @SxMn_GetCoord 3 index 11@ start_point 159.0 offset 39.0 ret 22@ // ypos = 159 + (y-1)*39
        if 003B:   20@ == 30@  // (int)
        then 038E: draw_box_position 21@ 22@ size 62.0 36.0 RGBA 0 0 160 120 // highlight selected
        end
        if 0AB1: @CursorClickCheck 4 box_position 21@ 22@ box_size 62.0 36.0
        then
            0085: 30@ = 20@ // (int)
            0006: 31@ = 0
        end
    end
end
for 11@ = 1 to 5
    for 10@ = 1 to 5
        0AB1: @SxMn_GetIntegerFromArray 3 x_val 10@ y_val 11@ width 5 ret 20@ // i = x + (y-1)*5 - 1
        0AB1: @SxMn_GetCoord 3 index 10@ start_point 306.0 offset 66.0 ret 21@ // xpos = 306 + (x-1)*66
        0AB1: @SxMn_GetCoord 3 index 11@ start_point 159.0 offset 39.0 ret 22@ // ypos = 159 + (y-1)*39
        if 003B:   20@ == 31@  // (int)
        then 038E: draw_box_position 21@ 22@ size 62.0 36.0 RGBA 0 0 160 120 // highlight selected
        end
        0AB1: @SxMn_GetGXTPose 2 place 30@ offset 20@ ret 12@ 13@
        if 85AE:   not 12@s == "______" // @s == 'short'
        then
            if 0AB1: @CursorClickCheck 4 box_position 21@ 22@ box_size 62.0 36.0
            then 0085: 31@ = 20@ // (int)
            end
        end
    end
end
0AB1: @StoreInt 2 @SxMn_Place 30@
0AB1: @StoreInt 2 @SxMn_Pose 31@
0AB2: ret 0

:SxMn_Place
hex
    0
end

:SxMn_Pose
hex
    0
end

{======================================================================
======================================================================}
:ShowSexualMenu2
// Menu left
038E: draw_box_position 75.0 317.0 size 150.0 263.0 RGBA 0 0 0 100
038E: draw_box_position 78.0 314.0 size 156.0 269.0 RGBA 0 0 0 100
038E: draw_box_position 83.0 223.0 size 167.0 49.0 RGBA 238 71 181 50
038E: draw_box_position 80.0 223.0 size 161.0 62.0 RGBA 238 71 181 50
038E: draw_box_position 83.0 229.0 size 167.0 20.0 RGBA 238 71 181 50
038E: draw_box_position 98.0 351.0 size 91.0 194.0 RGBA 0 0 0 110
038E: draw_box_position 98.0 189.0 size 91.0 6.0 RGBA 0 0 0 110

// Menu right
038E: draw_box_position 560.0 224.0 size 160.0 448.0 RGBA 0 0 0 60
038E: draw_box_position 556.0 224.0 size 168.0 448.0 RGBA 0 0 0 60
038E: draw_box_position 532.0 224.0 size 83.0 448.0 RGBA 0 0 0 80
038E: draw_box_position 556.0 430.0 size 168.0 10.0 RGBA 0 0 0 50
038E: draw_box_position 553.0 390.0 size 174.0 70.0 RGBA 238 71 181 50
038E: draw_box_position 548.0 390.0 size 183.0 60.0 RGBA 238 71 181 60
038E: draw_box_position 550.0 400.0 size 180.0 26.0 RGBA 238 71 181 80
038E: draw_box_position 531.0 139.0 size 136.0 235.0 RGBA 0 0 0 50

0AB1: @SexualMenu_GetContent 3 SexPoseInt SexPlace FLAG_SX_MENU_LEFT

if SexMenuState >= SX_MENU_OPT1_PLACES_FIRST_ELEM
then    
    // Highlighting right title
    038E: draw_box_position 548.0 390.0 size 173.0 70.0 RGBA 255 255 255 40 // White
    038E: draw_box_position 548.0 410.0 size 173.0 30.0 RGBA 255 255 255 40 // White

    0085: Dummy1 = SexMenuState // (int)
    
    // Is current selector on opt 1 right menu items ?
    if and
    0029:   SexMenuState >= SX_MENU_OPT1_PLACES_FIRST_ELEM
    002B:   SX_MENU_OPT1_PLACES_LAST_ELEM >= SexMenuState  // (int)
    then
        Dummy1 -= SX_MENU_OPT1_PLACES_FIRST_ELEM
        0AB1: @SexualMenu_GetContent 4 SexPoseInt SexPlace FLAG_SX_MENU_RIGHT SX_MENU_OPT1_PLACES_FIRST_ELEM
    end
        
    // Is current selector on opt 2 right menu items ?
    if and
    0029:   SexMenuState >= SX_MENU_OPT2_POSITIONS_FIRST_ELEM
    002B:   SX_MENU_OPT2_POSITIONS_LAST_ELEM >= SexMenuState  // (int)
    then
        Dummy1 -= SX_MENU_OPT2_POSITIONS_FIRST_ELEM
        0AB1: @SexualMenu_GetContent 4 SexPoseInt SexPlace FLAG_SX_MENU_RIGHT SX_MENU_OPT2_POSITIONS_FIRST_ELEM
    end
        
    // Is current selector on opt 3 right menu items ?
    if and
    0029:   SexMenuState >= SX_MENU_OPT3_CAMERAS_FIRST_ELEM
    002B:   SX_MENU_OPT3_CAMERAS_LAST_ELEM >= SexMenuState  // (int)
    then
        Dummy1 -= SX_MENU_OPT3_CAMERAS_FIRST_ELEM
        0AB1: @SexualMenu_GetContent 4 SexPoseInt SexPlace FLAG_SX_MENU_RIGHT SX_MENU_OPT3_CAMERAS_FIRST_ELEM
    end
    // Highlighting right selected item
    0093: Dummy1 = integer Dummy1 to_float
    Dummy1 *= 20.0
    Dummy1 += 46.0
    038E: draw_box_position 556.0 Dummy1 size 168.0 24.0 RGBA 0 0 160 120 // Dark blue
else
    // Highlighting left title
    038E: draw_box_position 83.0 223.0 size 166.0 62.0 RGBA 255 255 255 40 // White
    038E: draw_box_position 83.0 240.0 size 166.0 28.0 RGBA 255 255 255 50 // White
    
    // Highlighting left selected item
    0085: Dummy1 = SexMenuState // (int)
    Dummy1 -= 1
    0093: Dummy1 = integer Dummy1 to_float
    Dummy1 *= 20.0
    Dummy1 += 276.0
    038E: draw_box_position 78.0 Dummy1 size 156.0 24.0 RGBA 0 0 160 80 // Dark blue
end
return

{======================================================================
======================================================================}
:SexualMenu_GetContent
const
    SmGc_Pose = 0@
    SmGc_Place = 1@
    SmGc_LftOrRghtFlag = 2@
    SmGc_RightMenuFlag = 3@
    {4@, 5@}
    {6@s - 8@s - .... - 24@s: Array of 10 strings}
    SmGc_AuxStr = 26@s
    SmGc_Aux1 = 26@
    SmGc_Aux2 = 27@
    SmGc_MaxOpts = 28@
    SmGc_Index = 29@
    SmGc_PosY = 30@
    {31@}
end

// Read Content
if SmGc_LftOrRghtFlag == FLAG_SX_MENU_LEFT
then 0AB1: @GetLeftMenuElements 0 SmGc_MaxOpts 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@
else 0AB1: @GetRightMenuElements 1 SmGc_RightMenuFlag SmGc_MaxOpts 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@
end

// Title
if SmGc_LftOrRghtFlag == FLAG_SX_MENU_LEFT
then 0AB1: @SetDrawText 8 0 600.0 size 0.6 2.6 pos 6.0 205.0 GXT 4@ 5@
else 0AB1: @SetDrawText 8 0 640.0 size 0.8 2.7 pos 448.0 378.0 GXT 4@ 5@
end

// Options
if SmGc_LftOrRghtFlag == FLAG_SX_MENU_LEFT
then SmGc_PosY = 270.0
else SmGc_PosY = 40.0
end
for SmGc_Index = 0 to 28@ // 28@ : SmGc_MaxOpts 
    05AA: SmGc_AuxStr = 6@(SmGc_Index,12s) // @s = 'short'
    if SmGc_LftOrRghtFlag == FLAG_SX_MENU_LEFT
    then 0AB1: @SetDrawText 8 2 600.0 size 0.2 1.2 pos 10.0 SmGc_PosY GXT SmGc_Aux1 SmGc_Aux2
    else
        if SmGc_RightMenuFlag == SX_MENU_OPT1_PLACES_FIRST_ELEM
        then
            if 0AB1: @CheckPoseAndPlaceAvailability 2 SmGc_Pose SmGc_Index
            then 0AB1: @SetDrawText 8 2 600.0 size 0.2 1.2 pos 492.0 SmGc_PosY GXT SmGc_Aux1 SmGc_Aux2
            else 0AB1: @SetDrawTextRGB 11 2 600.0 size 0.2 1.2 pos 492.0 SmGc_PosY GXT SmGc_Aux1 SmGc_Aux2 100 100 100
            end
        end
        if SmGc_RightMenuFlag == SX_MENU_OPT2_POSITIONS_FIRST_ELEM
        then
            0AB1: @GetTruePositionIndex 1 SmGc_Index 31@
            if 0AB1: @CheckPoseAndPlaceAvailability 2 31@ SmGc_Place
            then 0AB1: @SetDrawText 8 2 600.0 size 0.2 1.2 pos 492.0 SmGc_PosY GXT SmGc_Aux1 SmGc_Aux2
            else 0AB1: @SetDrawTextRGB 11 2 600.0 size 0.2 1.2 pos 492.0 SmGc_PosY GXT SmGc_Aux1 SmGc_Aux2 100 100 100
            end
        end
        if SmGc_RightMenuFlag == SX_MENU_OPT3_CAMERAS_FIRST_ELEM
        then
        end
    end
    SmGc_PosY += 20.0
end
0AB2: ret 0

:GetTruePositionIndex
10@=SEX_CUNNI
11@=SEX_69
12@=SEX_BJ
13@=SEX_HJ
14@=SEX_FUCKING
15@=SEX_ANAL
16@=SEX_TJ
17@=SEX_FJ
0AB2: ret 1 10@(0@,8i)

{======================================================================
======================================================================}
:GetLeftMenuElements
4@s = 'S_MENU' // Sex Options
6@s = 'S_OPT1' // Cambiar lugar
8@s = 'S_OPT2' // Cambiar posicion
10@s = 'S_OPT3' // Cambiar camara
1@ = 2 // 3 gxt sin incluir título - disminuir en 1 para el ciclo "for"
0AB2: ret 23 1@ 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@

{======================================================================
======================================================================}
:GetRightMenuElements
if 0@ == SX_MENU_OPT1_PLACES_FIRST_ELEM
then
    4@s = 'S_MENU1' // Lugares
    6@s = 'S_PL_1' // Cama
    8@s = 'S_PL_2' // Sofa
    10@s = 'S_PL_3' // Mesa
    12@s = 'S_PL_4' // Silla
    14@s = 'S_PL_5' // Piso
    16@s = 'S_PL_6' // Baño
    18@s = 'S_PL_7' // Cocina
    20@s = 'S_PL_8' // Pared
    22@s = 'S_PL_9' // De pié
    1@ = 8 // 9 gxt sin incluir título - disminuir en 1 para el ciclo "for"
end
if 0@ == SX_MENU_OPT2_POSITIONS_FIRST_ELEM
then
    4@s = 'S_MENU2' // Posiciones
    6@s = 'S_CN_1' // Cunnilingus
    8@s = 'S_69_1' // Oral 69
    10@s = 'S_BJ_1' // Blowjob
    12@s = 'S_HJ_1' // Handjob
    14@s = 'S_FK_1' // Sex
    16@s = 'S_AN_1' // Reverse Sex
    18@s = 'S_TJ_1' // Titjob
    20@s = 'S_FJ_1' // Footjob
    1@ = 7 // 8 gxt sin incluir título - disminuir en 1 para el ciclo "for"
end
{if 0@ == SX_MENU_OPT3_CAMERAS_FIRST_ELEM
then
    4@s = 'S_MENU3' // Camaras
    6@s = '' //
    8@s = '' //
    10@s = '' //
    12@s = '' //
    14@s = '' //
    16@s = '' //
    18@s = '' //
    20@s = '' //
    22@s = '' //
    24@s = '' //
    1@ = 9 // 10 gxt sin incluir título - disminuir en 1 para el ciclo "for"
end}
0AB2: ret 23 1@ 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@

{======================================================================
======================================================================}
:GetRightSelectedElement
// 0@: right menu flag, 1@: selected index
0AB1: @GetRightMenuElements 1 0@ ret dummy_value 3@ string_array 4@ 5@ 6@ 7@ 8@ 9@ 10@ 11@ 12@ 13@ 14@ 15@ 16@ 17@ 18@ 19@ 20@ 21@ 22@ 23@ 24@ 25@
05AA: 30@s = 6@(1@,10s) // @s = 'short'
0AB2: ret 2 30@ 31@

{======================================================================
    Orgasm-o-meter bar                        Speed-o-meter bar
    min -> posX 170.5 sizeX 1.0               min -> posX 243.5 sizeX 1.0
    min+1 -> posX 171.0 sizeX 2.0             min+1 -> posX 244.0 sizeX 2.0
    min+2 -> posX 171.5 sizeX 3.0             min+2 -> posX 244.5 sizeX 3.0
    min+3 -> posX 172.0 sizeX 4.0             min+3 -> posX 245.0 sizeX 4.0
    100% -> posX 260.0 sizeX 180.0            100% -> posX 296.0 sizeX 106.0
======================================================================}
:ShowSexualStats
const
    Flt_Climax = 0@
    ShSt_Speed = 1@
    ShSt_Pleasure = 2@
    Flt_Speed = 4@
    Climax_PosBar = 5@
    Climax_SizeBar = 6@
    Speed_PosBar = 7@
    Speed_SizeBar = 8@
end
0093: Flt_Speed = integer ShSt_Speed to_float

//Climax_SizeBar = 1.8 * Flt_Climax
//Climax_PosBar = Climax_SizeBar/2 + 170
0087: Climax_SizeBar = Flt_Climax // (float)
0013: Climax_SizeBar *= 1.8
0087: Climax_PosBar = Climax_SizeBar // (float)
0013: Climax_PosBar *= 0.5
000B: Climax_PosBar += 170.0

//Speed_SizeBar = 1.06 * Flt_Speed
//Speed_PosBar = Speed_SizeBar/2 + 243
0087: Speed_SizeBar = Flt_Speed // (float)
0013: Speed_SizeBar *= 1.06
0087: Speed_PosBar = Speed_SizeBar // (float)
0013: Speed_PosBar *= 0.5
000B: Speed_PosBar += 243.0

//Black styled climax bar
{038E: draw_box_position 260.0 414.0 size 186.0 23.0 RGBA 0 0 0 100
038E: draw_box_position 260.0 414.0 size 180.0 16.0 RGBA 0 0 0 100
038E: draw_box_position 261.0 419.0 size 186.0 13.0 RGBA 0 0 0 80
038E: draw_box_position Climax_PosBar 414.0 size Climax_SizeBar 16.0 RGBA 0 0 0 250}

//Pink styled climax bar
038E: draw_box_position 260.0 414.0 size 186.0 23.0 RGBA 238 71 181 100
038E: draw_box_position 260.0 414.0 size 180.0 16.0 RGBA 0 0 0 100
038E: draw_box_position 260.0 419.0 size 186.0 13.0 RGBA 0 0 0 150
038E: draw_box_position Climax_PosBar 414.0 size Climax_SizeBar 16.0 RGBA 238 71 181 60 // the orgasm-o-meter bar
038E: draw_box_position Climax_PosBar 411.0 size Climax_SizeBar 3.0 RGBA 255 255 255 50 // the orgasm-o-meter bar 2

// Speedometer
038E: draw_box_position 257.0 436.0 size 1.0 15.0 RGBA 153 153 153 180 // Medium Gray
038E: draw_box_position 298.0 436.0 size 1.0 15.0 RGBA 153 153 153 180
038E: draw_box_position 337.0 436.0 size 1.0 15.0 RGBA 153 153 153 180
038E: draw_box_position 296.0 436.0 size 114.0 12.0 RGBA 238 71 181 80 // Pink
038E: draw_box_position 296.0 436.0 size 110.0 8.0 RGBA 0 0 0 140 // Black
038E: draw_box_position Speed_PosBar 436.0 size Speed_SizeBar 4.0 RGBA 255 0 0 250 // Red // the speed-o-meter bar

// Shadow on text "pleasure"
038E: draw_box_position 203.0 436.0 size 72.0 12.0 RGBA 0 0 0 120
    
0349: set_text_draw_font 2
0343: set_text_draw_linewidth 600.0
033F: set_text_draw_letter_size 0.2 1.0
033E: set_draw_text_position 179.0 409.0 GXT 'S_HUD1' // Climax

0349: set_text_draw_font 1
0343: set_text_draw_linewidth 600.0
033F: set_text_draw_letter_size 0.2 1.0
045A: draw_text_1number 171.0 430.0 GXT 'S_HUD2' number ShSt_Pleasure  // Pleasure: ~1~%
0AB2: ret 0

{======================================================================
======================================================================}
:ShowSettingTexts
// 0@: sex menu state, 1@: speed, 2@: pose, 3@ place

10@s = 'CFG_T3' // ~b~~k~~VEHICLE_ENTER_EXIT~: ~s~Go back.
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 24.0 12.0 GXT 10@ 11@
10@s = 'CFG_T1' // ~b~Mouse wheel~w~: Increase/decrease speed.
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 24.0 26.0 GXT 10@ 11@
10@s = 'CFG_T4' // ~b~~k~~CAMERA_CHANGE_VIEW_ALL_SITUATIONS~~s~: Change Camera mode. ~b~~k~~GO_LEFT~~s~/~b~~k~~GO_RIGHT~~s~: Change cam view (only in fixed mode).
0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 24.0 40.0 GXT 10@ 11@

{// Debug
0AB1: @GetData 1 @FirstPersonCameraVars 29@
if 056D:   actor 29@ defined
then
    0AB1: @GetData 2 @FirstPersonCameraVars 4 30@
    0172: 31@ = actor 29@ Z_angle
    0AD0: show_formatted_text_lowpriority "rotation: %f, angle: %f" time 1 30@ 31@
end}

0AB1: @GetInt 1 @CameraMode 8@
if 8@ == 0 // Free Camera
then
    10@s = 'CFG_C4' // Free Camera
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 169.0 390.0 GXT 10@ 11@
end
if 8@ == 1 // Fixed Camera
then
    0AB1: @GetInt 1 @CurrentCamera 9@
    9@+=1
    10@s = 'CFG_C5' // Fixed Camera ~y~~1~
    0AB1: @SetDrawTextRGB_1Number 12 font 2 linewidth 600.0 size 0.2 1.0 position 169.0 390.0 GXT 10@ 11@ rgb 255 255 255 number 9@
    10@s = 'CFG_T6' // <  >
    0AB1: @SetDrawText 8 font 1 linewidth 600.0 size 0.2 1.0 position 244.0 390.0 GXT 10@ 11@
end
if 8@ == 2 // First Person Camera
then
    10@s = 'CFG_C6' // First Person
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.0 position 169.0 390.0 GXT 10@ 11@
end

// 8@: selected animation, 9@: max animations for current pose
10@s = 'INT_1' // ~1~
0AB1: @GetInt 1 @CurrentAnimation 8@
0AB1: @ReadSexPosesInfo 3 1@ 2@ 3@ out 12@ 13@ 9@
for 12@ = 1 to 9@
    if 003B:   12@ == 8@  // (int)
    then 13@=100
    else 13@=255
    end
    14@=18.0
    0093: 16@ = integer 12@ to_float
    006B: 14@ *= 16@  // (float)
    15@=-18.0
    0093: 16@ = integer 9@ to_float
    006B: 15@ *= 16@  // (float)
    005B: 14@ += 15@  // (float)
    14@ += 340.0      // x-pos = 18*index - 18*max-anims + 340
    0AB1: @SetDrawTextRGB_1Number 12 font 1 linewidth 600.0 size 0.4 1.2 position 14@ 389.0 GXT 10@ 11@ rgb 13@ 13@ 13@ number 12@
end

if 0@ <= 0 // sex menu closed/hidden
then
    //10@s = 'CFG_T2' // ~b~~k~~PED_ANSWER_PHONE~~w~: Open/close menu.
    10@s = 'CFG_T7' // Change position
    0AB1: @SetDrawText 8 font 2 linewidth 600.0 size 0.2 1.2 position 10.0 430.0 GXT 10@ 11@
end
0AB2: ret 0
